{
  pkgs,
  config,
  inputs,
  ...
}:
let
  authenticationConfig = (pkgs.formats.yaml { }).generate "k3s-authentication-config.yaml" {
    apiVersion = "apiserver.config.k8s.io/v1";
    kind = "AuthenticationConfiguration";
    jwt = [
      {
        issuer = {
          url = "https://git.ncrmro.com/api/actions";
          audiences = [ "kubernetes-ocean" ];
        };
        claimValidationRules = [
          {
            expression = ''
              has(claims.sub) &&
              has(claims.repository) &&
              has(claims.event_name) &&
              has(claims.workflow_ref)
            '';
            message = "required Forgejo Actions deployment claims are missing";
          }
          {
            expression = "claims.exp - claims.nbf <= 3600";
            message = "token lifetime must not exceed one hour";
          }
        ];
        claimMappings = {
          username.expression = "'oidc:forgejo:' + claims.sub";
          groups.expression = ''
            ['oidc:forgejo:repo:' + claims.repository +
            ':subject:' + claims.sub +
            ':event:' + claims.event_name +
            ':workflow:' + claims.workflow_ref]
          '';
        };
        userValidationRules = [
          {
            expression = "!user.username.startsWith('system:')";
            message = "username must not use the reserved system prefix";
          }
          {
            expression = "user.groups.all(group, !group.startsWith('system:'))";
            message = "groups must not use the reserved system prefix";
          }
        ];
      }
    ];
  };
in
{
  # Define the K3s server token secret
  age.secrets.k3s-server-token = {
    file = "${inputs.agenix-secrets}/secrets/k3s-server-token.age";
    owner = "root";
    group = "root";
    mode = "0400";
  };
  # containerd configuration
  virtualisation.containerd = {
    enable = true;
    settings =
      let
        fullCNIPlugins = pkgs.buildEnv {
          name = "full-cni";
          paths = with pkgs; [
            cni-plugins
            cni-plugin-flannel
          ];
        };
      in
      {
        version = 2;
        plugins."io.containerd.grpc.v1.cri".containerd = {
          snapshotter = "zfs";
        };
        plugins."io.containerd.grpc.v1.cri".cni = {
          bin_dir = "${fullCNIPlugins}/bin";
          conf_dir = "/var/lib/rancher/k3s/agent/etc/cni/net.d/";
        };
        # Optionally set private registry credentials here instead of using /etc/rancher/k3s/registries.yaml
        # plugins."io.containerd.grpc.v1.cri".registry.configs."registry.example.com".auth = {
        #   username = "";
        #   password = "";
        # };
      };
  };

  # ZFS LocalPV runs from a Kubernetes HelmChart managed in k8s-cluster, but
  # its node plugin still needs the host's ZFS tools at conventional paths.
  environment.systemPackages = [ pkgs.zfs ];
  systemd.services.zfs-usr-bin = {
    description = "ZFS symlinks in /usr/bin for OpenEBS ZFS LocalPV";
    after = [ "network.target" ];
    wantedBy = [ "multi-user.target" ];
    serviceConfig = {
      Type = "oneshot";
      ExecStart = [
        "${pkgs.coreutils}/bin/mkdir -p /usr/bin"
        "${pkgs.coreutils}/bin/ln -sf ${pkgs.zfs}/bin/zfs /usr/bin/zfs"
        "${pkgs.coreutils}/bin/ln -sf ${pkgs.zfs}/bin/zpool /usr/bin/zpool"
      ];
      RemainAfterExit = true;
    };
  };

  # k3s configuration
  networking.firewall = {
    # Pods reach NixOS-hosted services via the node address (see
    # k3s-host-services.nix Endpoints); that traffic ingresses on the CNI
    # bridge, which the firewall would otherwise drop.
    trustedInterfaces = [ "cni0" ];
    # Open K3s cluster ports only on Tailscale interface
    interfaces.tailscale0 = {
      allowedTCPPorts = [
        6443 # k3s: API server (restricted to Tailscale only)
        10250 # k3s: kubelet API
      ];
      allowedUDPPorts = [
        8472 # k3s: flannel VXLAN
      ];
    };
  };
  services.k3s.enable = true;
  services.k3s.role = "server";
  services.k3s.tokenFile = config.age.secrets.k3s-server-token.path;
  environment.etc."rancher/k3s/authentication-config.yaml".source = authenticationConfig;
  services.k3s.extraFlags = toString [
    "--disable=traefik" # Disable traefik to use ingress nginx instead
    "--disable=local-storage"
    "--container-runtime-endpoint=/run/containerd/containerd.sock"
    "--write-kubeconfig-mode=0644"
    "--tls-san=ocean.mercury"
    "--tls-san=100.64.0.6"
    "--node-ip=100.64.0.6"
    "--flannel-iface=tailscale0"
    "--kube-apiserver-arg=authentication-config=/etc/rancher/k3s/authentication-config.yaml"
    # "--debug" # Optionally add additional args to k3s
  ];
  systemd.services.k3s.restartTriggers = [ authenticationConfig ];
}
